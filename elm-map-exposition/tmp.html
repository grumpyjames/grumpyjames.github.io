<h3 id="combining-two-ideas">Combining two ideas</h3>
<p>Previously, we wrote a lazy image loader, and a very simple tiling function. Now we want to combine them.</p>
<p>Our goal is to render a tiling of web mercator images, with each image being lazily loaded. Imagine the previous tiling demo, but instead of rendering the tile's coordinate we lazily load the equivalent map tile corresponding to that coordinate.</p>
<p>We'll also provide some basic controls so we can change the origin of the resulting tiling.</p>
<p>Let's have a think about how to do this.</p>
<pre><code>
type alias TilingInstruction a = 
    { rowCount: Int
    , columnCount: Int
    , origin: Tile
    , view: Tile -> Html a
    }
</code></pre>
<p>It's clear that most of our change, from the previous demo, is going to be in the implementation of <code>view</code>. We'll also hold on to a bit more state in our model, which looks like this.</p>
<pre><code>
type alias Model = 
    { rowCount: Int
    , columnCount: Int
    , origin: Tiler.Tile
    , loadedImages : Dict (Int, Int) Url
    }
</pre>
<p></code></p>
<p>The events we're going to handle will also widen a little; we'll add the co-ordinate of the tile to the 'image loaded' event, and we'll also add an event to encapsulate a shift in the map's origin.</p>
<pre><code>
type Msg = Complete (Int, Int) Url
         | Shift (Int, Int)
</pre>
<p></code></p>
<p>Let's look at how we define our view.</p>
<pre><code>
view : Model -> Html Msg
view m =
    let tiles = Tiler.tile (TilingInstruction m.rowCount m.columnCount m.origin (loadingTileImages m.loadedImages))
    in Html.div [] [controls, tiles]
</code></pre>
<p>We'll have some controls, and some tiles. The tiles are the bit we're interested in. We're invoking exactly the same <code>tile</code> function that was introduced in part two, but this time, the instruction we are passing it is slightly more dynamic.</p>
<p>The type of <code>view</code> in <code>TilingInstruction</code> is <code>Tile -&gt; Html a</code>. It looks like <code>loadingTileImages m.loadedImages</code>. is going to have to be pretty clever.</p>
<pre><code>
loadingTileImages : Dict (Int, Int) Url -> Tiler.Tile -> Html Msg
loadingTileImages cache tile =
    let lookup = Dict.get (tile.x, tile.y) cache
    in 
      case lookup of
        Just url -> readyImage url
        Nothing -> loadingImage (tile.x, tile.y) (imageUrl tile)
</code></pre>
<p>...handily though, it is actually quite simple: we're <em>partially applying</em> this function, binding the loading images into place for each invocation. At the point where the tiler requests a view for a particular tile, we'll check to see if we've already loaded it, and we'll pick a rendering based on that state.</p>
<p>Our definitions of <code>loadingImage</code> and <code>readyImage</code> are very similar to the original <code>LazyLoader</code> demo with the following difference:</p>
Before:
<pre><code>onWithOptions "load" (Options False False) (succeed (Complete url))</code></pre>
After:
<pre><code>onWithOptions "load" (Options False False) (succeed (Complete coordinate url))</code></pre>
<p>This way, when the load event arrives, handling it is much simpler.</p>
<pre><code>
update : Msg -> Model -> Model
update message model =
    case message of
      Complete key value ->
          { model | loadedImages = Dict.insert key value model.loadedImages }
</code></pre>
<p>Very simple.</p>
<p>Adding the controls is...tedious but effective:</p>
<pre><code>
controls : Html Msg
controls = 
    let shiftButton shift text = Html.button [(Html.Events.on "click" (succeed (Shift shift)))] [Html.text text]
        upButton = shiftButton (0, -1) "North"
        downButton = shiftButton (0, 1) "South"
        leftButton = shiftButton (-1, 0) "West"
        rightButton = shiftButton (1, 0) "East"
    in Html.div [] [upButton, downButton, leftButton, rightButton]
</code></pre>
<p>...and handling the <code>Shift</code> messages this sends is again trivial, here's the other half of our update function:</p>
<pre><code>
      Shift diff -> 
          { model | origin = shift diff model.origin }

shift : (Int, Int) -> Tiler.Tile -> Tiler.Tile
shift (dx, dy) tile =
    Tiler.Tile (tile.x + dx) (tile.y + dy) 
</code></pre>
<p>We'll provide a function, <code>imageUrl : Tiler.Tile -&gt; Url</code>, that knows how to craft a <code>URL</code> that corresponds to appropriate web mercator tiles source from <a href="http://www.mapbox.com">MapBox</a>, and we'll have our next demo.</p>
<p>Author's note: an extended battle with HTML/CSS followed here after realising that this demo worked rather poorly on viewports with a width smaller than 1024px. HTML/CSS won, so we're going to have to do some more work.</p>
<p>To cut a long story short, the <code>div</code> that the <code>Tiler</code> creates to house each row of tiles will need to be given an explicit width to prevent non-fitting tile images from skipping on to the next line. Ugh.</p>
<h3 id="widening-tilinginstruction">Widening <code>TilingInstruction</code></h3>
<pre><code>
type alias TilingInstruction a = 
    { rowCount: Int
    , columnCount: Int
    , origin: Tile
    , viewTile: Tile -> Html a
    , viewRow: List (Html a) -> Html a
    }
</code></pre>
<p>Previously, our <code>tile</code> function handily dropped our elements into appropriate <code>div</code>s and we were done. Now we have to get each row's <code>div</code> to have a fixed width, so we pass a row viewer, as well as a tile viewer. We should probably think about whether <code>tile</code> really wants to know about <code>Html</code> at some point, but this was the easiest way to get things to work in any given browser window for this post.</p>
<p>Here's what our <code>view</code> function looks like now:</p>
<pre><code>
view : Model -> Html Msg
view m =
    let tiles = 
            Tiler.tile { rowCount = m.rowCount
                       , columnCount = m.columnCount
                       , origin = m.origin
                       , viewTile = (loadingTileImages m.images)
                       , viewRow = fixedWidth
                       }
    in Html.div [] [controls, tiles]

px : Int -> String
px pixels = (toString pixels) ++ "px"

fixedWidth : List (Html a) -> Html a
fixedWidth htmls = 
    let width = (List.length htmls) * 256
    in Html.div [style [("width", (px width))]] htmls
</code></pre>
<p>...and finally, here is the <a href="demo-3.1.html">demo</a></p>
